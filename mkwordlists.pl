#!/usr/bin/env perl

use 5.010;
use strict;
use warnings;

use utf8;
use open qw(:std :utf8);

my %words = ();

for my $file ( <wordlists/*.txt> ) {
    my $lang = $file =~ s/wordlists\/(.*)\.txt/$1/r;
    my @list = map { chomp; $_ } do {
        open my $in, '<', $file or die $!;
        <$in>
    };
    die "Wordlist for $lang doesn't contain 2048 words" unless @list == 2048;
    $words{$lang} = \@list;
}

open my $out, '>', './src/wordlists.c' or die $!;

print $out <<~";;";
    /* This file is generated by mkwordlist.pl */

    #include <stddef.h>
    #include "wordlist.h"

    @{[
        join "\n", map {
            my $lang = $_;
            my $list = $words{$lang};
            "static const char* const wordlist_${lang} = " . (
                join "", map { "\"$_\\0\"" } @$list
            ) . ";\n"
        } sort keys %words
    ]}
    const wordlist_t wordlists[] = {
        @{[ join ",", map { "{ \"$_\", wordlist_$_ }" } sort keys %words ]},
        { NULL, NULL }
    };
    ;;

close $out;

system 'clang-format', '-i', './src/wordlists.c';
